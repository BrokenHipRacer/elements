
allprojects {
    group 'net.e6tech'
    version '1.0-SNAPSHOT'

    apply plugin: 'java'

    sourceCompatibility = 1.8

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:syntax', '-quiet')
    }

    repositories {
        mavenCentral()
    }
}

dependencies {
    testCompile("org.junit.jupiter:junit-jupiter-api:5.0.3")
    testCompile("org.junit.platform:junit-platform-launcher:1.0.3")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.3")
}

subprojects {

    group 'net.e6tech'
    version '2.5.0'

    apply plugin: 'groovy'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'maven'
    apply plugin: 'signing'

    archivesBaseName = "elements-${project.name}"

    signing {
        sign configurations.archives
    }

    javadoc {
        source = sourceSets.main.allJava
        classpath = configurations.compile
    }

    task sourcesJar(type: Jar) {
        archiveBaseName.set "elements-${project.name}"
        archiveClassifier.set 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            common(MavenPublication) {
                from components.java
                artifactId "elements-${project.name}"

                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name project.name
                    description "elements-${project.name}"
                    packaging 'jar'
                    // optionally artifactId can be defined here. description 'A application used as an example on how to set up pushing its components to the Central Repository.'
                    url 'http://github.com/futehkao/elements.git'

                    scm {
                        connection 'scm:git:https://github.com/futehkao/elements.git'
                        developerConnection 'scm:git:https://github.com/futehkao/elements.git'
                        url 'https://github.com/futehkao/elements.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'futeh'
                            name 'Futeh Kao'
                            email 'futeh@episodesix.com'
                        }
                    }
                }
            }
        }
    }
}

ext.junitPlatformLauncherVersion = '1.4.1'
ext.junitJupiterVersion = '5.4.1'
ext.snakeyamlVersion = '1.24'
ext.simplejndiVersion = '0.11.4.1'
ext.findbugsVersion = '3.0.2'
ext.lmaxDisruptorVerions = '3.4.2'
ext.slf4jVersion = '1.7.26'
ext.groovyVersion = '2.5.7'
ext.guavaVerions = '28.0-jre'
ext.akkaVersion = '2.5.23'
ext.akkaScalaVersion = '2.12'
ext.akkaKryoVersion = '0.5.2'
ext.bytebuddyVersion = '1.9.13'
ext.ehcacheVersion = '3.7.1'
ext.javaCacheApiVerison = '1.1.1'
ext.jacksonVersion = '2.9.9'
ext.jettyVersion = '9.4.19.v20190610'
ext.tomcatVersion = '9.0.20'
ext.tomcatLog4jVersion = '9.0.0.M6'
ext.log4jVersion = '2.12.0'
ext.cxfVersion = '3.3.2'
ext.asmVersion = '7.1'
ext.javaxWsRsApiVersion = '2.1.5'
ext.dropwizardMetricsVersion = '4.0.5'
ext.cassandraDriverVersion = '3.7.2'
ext.opendmkVersion = '1.0-b02'
ext.simplejmxVersion = '1.17'
ext.quartzVersion = '2.3.1'
ext.hikariVersion = '3.3.1'
ext.mariadbClientVersion = '2.4.2'
ext.javaeeApiVersion = '8.0.1'
ext.hibernateVersion = '5.4.3.Final'
ext.bouncycastleVersion = '1.62'
ext.jaxbVersion = '2.3.0'